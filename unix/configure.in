dnl Process this file with autoconf to produce a configure script.
AC_INIT(../include/mk4.h)
AC_PREREQ(2.0)
AC_CONFIG_AUX_DIR(scripts)
AC_CONFIG_HEADER(config.h)
AC_LANG_CPLUSPLUS

MK_TARGETS="core"
MK_INSTALL="install-mk"

AC_ARG_ENABLE(python, [  --enable-python         build the Mk4py extension for Python], [python_ok=$enableval], [python_ok=no])

AC_MSG_CHECKING([for Python configuration])
if test "$python_ok" = yes; then
  MK_TARGETS="$MK_TARGETS python"
  MK_INSTALL="$MK_INSTALL install-python"
  AC_MSG_RESULT(enabled)
else
  AC_MSG_RESULT(not enabled)
fi

AC_ARG_WITH(tcl, [  --with-tcl=DIR          location of tcl.h header file], [TCL_INCLUDE_DIR=${withval}])

AC_MSG_CHECKING([for Tcl headers])
if test x"${TCL_INCLUDE_DIR}" != x ; then
  if test -f "${TCL_INCLUDE_DIR}/tcl.h" ; then
    AC_MSG_RESULT(found ${TCL_INCLUDE_DIR}/tcl.h)
    MK_TARGETS="$MK_TARGETS tcl"
    MK_INSTALL="$MK_INSTALL install-tcl"
  else
    AC_MSG_ERROR([${TCL_INCLUDE_DIR} directory doesn't contain tcl.h])
  fi
else
  TCL_INCLUDE_DIR="# no Tcl headers"
  AC_MSG_RESULT(not enabled)
fi

# Checks for programs.
AC_PROG_CXX
AC_PROG_INSTALL

# Libtool support
AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL

# Checks for libraries.

# Checks for header files.
AC_HEADER_STDC

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_CHECK_SIZEOF(long,4)
AC_CHECK_TYPES(long long)

# Checks for library functions.
AC_CHECK_FUNCS(mmap memmove bcopy)

AC_SUBST(MK_TARGETS)
AC_SUBST(MK_INSTALL)
AC_SUBST(TCL_INCLUDE_DIR)
AC_SUBST(EXEEXT)

AC_OUTPUT(Makefile)
